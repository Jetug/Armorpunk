import groovy.json.JsonSlurper

buildscript {
    repositories {
        // These repositories are only for Gradle plugins, put any other repositories in the repository block further below
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven { url = 'https://repo.spongepowered.org/maven' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath 'org.parchmentmc:librarian:1.+'
        classpath 'org.spongepowered:mixingradle:0.7-SNAPSHOT'
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

version = '1.0'
group = 'com.jetug.chassis_addon' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'chassis_addon'

// Mojang ships Java 17 to end users in 1.18+, so your mod should target Java 17.
java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"
minecraft {
//    mappings channel: 'parchment', version: '2022.11.06-1.18.2'

    mappings channel: 'official', version: '1.18.2'

//     accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg') // Currently, this location cannot be changed from the default.

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            workingDirectory project.file('run')

            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            property 'forge.logging.markers', 'REGISTRIES'
            property 'mixin.env.disableRefMap', 'true'
            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', 'chassis_addon'

            mods {
                chassis_addon {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'mixin.env.disableRefMap', 'true'
            property 'forge.logging.console.level', 'debug'

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            property 'forge.enabledGameTestNamespaces', 'chassis_addon'

            mods {
                chassis_addon {
                    source sourceSets.main
                }
            }
        }

        gameTestServer {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'mixin.env.disableRefMap', 'true'
            property 'forge.logging.console.level', 'debug'

            property 'forge.enabledGameTestNamespaces', 'chassis_addon'

            mods {
                chassis_addon {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            args '--mod', 'chassis_addon', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                chassis_addon {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { url 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/' }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    flatDir {
        dirs 'lib'
    }
}


dependencies {
    minecraft 'net.minecraftforge:forge:1.18.2-40.1.0'

    //implementation fg.deobf("curse.maven:chassis_addon-845956:4715444")

    //implementation files{'libs/geckolib-forge-1.18-3.0.57.jar'}

    implementation fg.deobf('com.jetug:chassis_core:1.0')

    implementation fg.deobf('software.bernie.geckolib:geckolib-forge-1.18:3.0.57')
    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
    testAnnotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, "chassis_addon.mixin-refmap.json"
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "chassis_addon",
                "Specification-Vendor"    : "chassis_addonsareus",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "chassis_addonsareus",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "TweakClass"              : "org.spongepowered.asm.launch.MixinTweaker",
                "TweakOrder"              : 0,
                "MixinConfigs"            : "chassis_addon.mixin.json"
        ])
    }
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}


void genAnimationNames(){
    def directoryPath = "src/main/resources/assets/chassis_addon/animations"
    def animationsTag = "animations"
    def directory = new File(directoryPath)
    def files = fileTree(directory)

    if (files) {
        files.each { file ->
            if (file.isFile() && file.name.endsWith(".json")) {
                def className = getClassName(file.name)
                def json = new JsonSlurper().parse(file)
                generateClassFile(className, json[animationsTag] as Map)
            }
        }
    }
}

void genAnimations(){
    def sourceDir = "src/main/resources/assets/chassis_addon/animations"
    generateResourceFile("Animations", sourceDir)
}

void genTextures(){
    def sourceDir = "src/main/resources/assets/chassis_addon/textures"
    generateResourceFile("Textures", sourceDir, true)
}

void genModels(){
    def sourceDir = "src/main/resources/assets/chassis_addon/geo"
    generateResourceFile("Models", sourceDir)
}

void genResourceUtilsFile() {
    def packageName = "com.jetug.generated"
    def generatedDir = "src/main/java/com/jetug/generated/"
    def directory = new File(generatedDir)

    def generatedCode = """package ${packageName};

import net.minecraft.resources.ResourceLocation;

public class ResourceUtils {
      public static ResourceLocation resourceLocation(String location){
        return new ResourceLocation("chassis_addon", location);
    }
}
"""

    if (!directory.exists()) directory.mkdirs()
    new File("${generatedDir}ResourceUtils.java").text = generatedCode
}

void generateResourceFile(String className, String directoryPath, boolean uni = false) {
    def packageName = "com.jetug.generated.resources"
    def generatedDir = "src/main/java/com/jetug/generated/resources/"
    def directory = new File(generatedDir)

    def constantsSection = generateResourceConstants(directoryPath, uni)

    def generatedCode = """package ${packageName};\n
import net.minecraft.resources.ResourceLocation;
import static com.jetug.generated.ResourceUtils.*;\n
public class ${className} {\n${constantsSection}}
"""

    if (!directory.exists()) directory.mkdirs()
    new File("${generatedDir}${className}.java").text = generatedCode
}

String generateResourceConstants(String directoryPath, boolean uni) {
    def directory = new File(directoryPath)
    def files = fileTree(directory)

    def constantsSection = new StringBuilder()
    def constant = "    public static final ResourceLocation"

    if (files) {
        files.each { file ->
            def name = getConstantName(getNameWithoutExt(file.name))

            if(file.parentFile.path != directory.path && uni)
                name = "${file.parentFile.name.toUpperCase()}_${name}"

            def text = file.path
            def sequence = "chassis_addon"

            def relativePath = text.substring(text.indexOf(sequence) + sequence.length()).replace("\\", "/")
            relativePath = relativePath.startsWith("/") ? relativePath.substring(1) : text

            constantsSection.append("${constant} ${name} = resourceLocation(\"${relativePath}\");\n")
        }
    }

    return constantsSection.toString()
}

static String getNameWithoutExt(String fileName){
    return fileName.replaceAll("\\..*", "")
}

static String getClassName(String fileName) {
    def className = fileName.replaceAll(/(\.[^.]+)$/, "").tokenize('_.')
    def capitalizedWords = className.collect { it.capitalize() }
    return capitalizedWords.join()
}

static void generateClassFile(String className, Map json) {
    def generatedDir = "src/main/java/com/jetug/generated/animations/"
    def packageName = "com.jetug.generated.animations"
    def constantsSection = generateConstantsSection(json)
    def generatedCode = "package ${packageName};\n\npublic class ${className} {\n${constantsSection}}"
    def dir = new File(generatedDir)

    if (!dir.exists()) dir.mkdirs()
    new File("${generatedDir}${className}.java").text = generatedCode
}

static String generateConstantsSection(Map json) {
    def constantsSection = new StringBuilder()
    def constant = "    public static final String"

    json.each { key, value ->
        constantsSection.append("${constant} ${getConstantName(key)} = \"${key}\";\n")
    }

    return constantsSection.toString()
}

static getConstantName(String name){
    return name.toUpperCase().replaceAll("\\s+|\\.", "_")
}

tasks.register('generateAnimations') {
    doFirst {
        genResourceUtilsFile()
        genAnimationNames()
        genAnimations()
        genTextures()
        genModels()
    }
}

compileJava {
    finalizedBy generateAnimations
}